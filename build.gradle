plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5' apply false
    id 'io.spring.dependency-management' version '1.1.3' apply false
}

java {
    sourceCompatibility = '17'
}

// root Project를 포함한 모든 프로젝트에 적용되는 설정
allprojects {
    group = projectGroup
    version = applicationVersion

    repositories {
        mavenCentral()
    }
}

// root Project를 제외한 모든 subProject에 적용되는 설정
subprojects {
    apply {
        plugin 'java'
        plugin 'io.spring.dependency-management'
        plugin 'org.springframework.boot'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependencyVersion}"
        }
    }

    dependencies {
        // web
        implementation 'org.springframework.boot:spring-boot-starter-web'

        // database
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'com.h2database:h2'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named("bootJar") {
        enabled = false
    }

    tasks.named("jar") {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}




